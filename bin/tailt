#!/usr/bin/perl

# Created on: 2010-09-14 08:51:00
# Create by:  Ivan Wills
# $Id$
# $Revision$, $HeadURL$, $Date$
# $Revision$, $Source$, $Date$

use strict;
use warnings;
use version;
use Scalar::Util;
use List::Util;
#use List::MoreUtils;
use Getopt::Alt qw/get_options/;
use Pod::Usage;
use Data::Dumper qw/Dumper/;
use English qw/ -no_match_vars /;
use FindBin qw/$Bin/;
use Path::Class;
use Tail::Tool;
use Term::Spinner;
use AnyEvent;
use EV;
use IO::Prompt qw/prompt/;
use TryCatch;

our $VERSION = version->new('0.0.1');
my ($name)   = $PROGRAM_NAME =~ m{^.*/(.*?)$}mxs;
my $spinner = Term::Spinner->new();

my %option = (
    lines   => 10,
);
my %found_plugins;
my $restart = {};
my $tt;

if ( !@ARGV ) {
    pod2usage( -verbose => 1 );
}

main();
exit 0;

sub main {
    my %plugins = get_plugins();

    my $opt = get_options(
        \%option,
        'restart|r!',
        'lines|n=i',
        'no_inotify!',
        'Highlight|highlight|h=s@',
        'Match|match|m=s@',
        'Ignore|ignore|i=s@',
        'Replace|replace|r=s@',
        ( map {  "$_|". lc $_. '=s%' } keys %plugins ),
        'verbose|v+',
        'man',
        'help',
        'VERSION!',
    );
    %option = %{ $opt->opt };

    # do stuff here
    for my $key (qw/Highlight Match Ignore Replace/) {
        next if !exists $option{$key};
        $option{$key} = { regex => [@{ $option{$key} }] };
    }

    if ( $option{restart} ) {
        $restart->{normal} = AE::io *STDIN, 0, sub {
            my $cmd = <STDIN>;
            chomp $cmd;

            exit 0 if lc $cmd eq 'q' || lc $cmd eq 'bye';

            return if $cmd eq '';

            restart();
        };
        $restart->{int} = AE::signal INT => \&restart;
        delete $option{restart};
    }

    $tt = Tail::Tool->new(
        files => \@ARGV,
        #printer => \&printer,
        %option,
    );

    $tt->tail();

    EV::loop;

    return;
}

sub restart {
    print "\n";
    my $files   = join ', ', map { $_->name } @{ $tt->files };
    my $plugins = '';
    my $i       = 0;
    my %done;
    my @plugins;

    for my $plg ( @{ $tt->pre_process }, @{ $tt->post_process }, sort keys %found_plugins ) {
        my $name = ref $plg || $plg;
        $name =~ s/^(.+::)//xms;

        next if !ref $plg && $done{$name};
        $i++;
        $done{$name} = $plg;
        $plugins[$i] = $plg;

        $plugins .= "\n" if $plugins;
        $plugins .= sprintf "%2d  %s %s", $i, $name eq $plg ? 'Add' : 'Change', $name;
        if ( ref $plg && $plg->can('summarise') ) {
            $plugins .= ' (' . $plg->summarise . ')';
        }
    }

    print <<"MENU";

$plugins
 f  Change tailed files ($files)
 r  Resume tailing
 q  Quit
MENU

    my $answer = prompt_menu( 1 .. $i, qw/f r q Q/ );

    if ( $answer eq 'f' ) {
        update_files();
    }
    elsif ( $answer eq 'r' ) {
        return 1;
    }
    elsif ( $answer =~ /^\d+$/ ) {
        update_plugin( $plugins[$answer] );
    }

    exit if !defined $answer || $answer eq '' || lc $answer eq 'q';

    return restart();
}

sub printer {
    my @lines = @_;
    if (@lines) {
        $spinner->advance;
    }
    else {
        $spinner->clear;
        print {*STDOUT} @lines;
    }
    die "Why isn't this working?\n".@lines."\n";
}

sub get_plugins {
    my %plugins;

    for my $inc (@INC) {
        my $dir = dir($inc, 'Tail', 'Tool', 'Plugin');
        next if !-d $dir;

        my @modules = grep { /[.]pm$/ } $dir->children;

        MODULE:
        for my $module (@modules) {
            my $name = $module->basename;
            $name =~ s/[.]pm//xms;
            next if $found_plugins{$name}++;

            eval { require $module };
            warn $@ if $@;
            next if $EVAL_ERROR;

            next MODULE if $name eq 'Highlight' || $name eq 'Ignore' || $name eq 'Match';

            $module =~ s{$inc/}{}xms;
            $module =~ s{[.]pm}{}xms;
            $module =~ s{/}{::}gxms;
            $plugins{$name} = $module->does('Tail::Tool::RegexRole');
        }
    }

    return %plugins;
}

sub update_files {
    my $i = 0;
    my $files = join "\n", map { sprintf "%2d  Change %s", ++$i, $_->name } @{ $tt->files };
    print <<"MENU";

$files
 a  Add another file to tail
 r  Return to previous menu
MENU

    my $answer = prompt_menu( 1 .. $i, qw/a r R/ );

    return if $answer eq 'r';

    if ( $answer eq 'a' ) {
        my $new_file = prompt("New file name : ", '-tty') . '';
        if ( -f $new_file ) {
            my $file = Tail::Tool::File->new( name => $new_file );
            push @{ $tt->files }, $file;
            $tt->tail( 1 );
        }
    }
    else {
        update_file( $answer - 1 );
    }
    return update_files();
}

sub update_file {
    my ($i) = @_;
    my $file = $tt->files->[$i];
    my $name = $file->name;
    my $pause = $file->pause ? 'Resume' : 'Pause';

    print <<"MENU";
 d  Delete $name
 p  $pause tailing of $name
 r  Return
MENU

    my $answer = prompt_menu( qw/d p r R/ );

    return if $answer eq 'r';

    if ( $answer eq 'p' ) {
        $file->pause( ! $file->pause );
    }
    elsif ( $answer eq 'd' ) {
        my @files = @{ $tt->files };
        if ( $i == 0 ) {
            shift @files;
        }
        elsif ( $i == @files - 1 ) {
            pop @files;
        }
        else {
            @files = ( @files[ 0 .. $i - 1], @files[ $i + 1 .. @files - 1 ] );
        }
        $tt->files(\@files);

        return;
    }

    return update_file($i);
}

sub update_plugin {
    my ($plg) = @_;

    my $plugin = $plg;
    if ( !ref $plugin ) {
        my $module = 'Tail::Tool::Plugin::' . $plugin;
        $plugin = $module->new();
    }

    my $meta = $plugin->meta;
    my $i = 0;
    my @names;

    for my $attrib ( $meta->get_all_attributes ) {
        my $name = $attrib->name;
        next if $name eq 'post';
        next if !$attrib->has_init_arg;
        next if $attrib->{isa} eq 'CodeRef';
        #next if grep { $name eq $_ } qw/last_time/;
        $i++;

        $names[$i] = $attrib;
        my $out = sprintf "%2d  Change $name", $i;

        my $reader = $attrib->reader || $name;
        my $value = $plugin->$reader();

        $out .= ' (' . show_value($value) . ')' if $value;

        print "$out\n";
    }
    print " a  Add a new instance\n" if $plg eq $plugin && $plugin->many;
    print " r  Return to previous menu\n";

    my $answer = prompt_menu( 1 .. $i, qw/a r R/ );

    return if !defined $answer || $answer eq '' || $answer eq 'r';

    if ( $answer eq 'a' ) {
        $plg = ref $plugin;
        $plg =~ s/^.*:://xms;
        return update_plugin( $plg );
    }

    my $updated = update_attribute( $plugin, $names[$answer] );

    if ( $updated && $plugin ne $plg ) {
        if ( $plugin->post ) {
            $tt->post_process( [ @{ $tt->post_process() }, $plugin ] );
        }
        else {
            $tt->pre_process( [ @{ $tt->pre_process() }, $plugin ] );
        }
    }

    return update_plugin($plugin);
}

sub update_attribute {
    my ( $plugin, $attrib ) = @_;
    my $name   = $attrib->name;
    my $reader = $attrib->reader || $name;
    my $writer = $attrib->writer || $name;
    my $value = $plugin->$reader();

    if ( ref $value eq 'ARRAY' ) {
        try {
            $plugin->$writer( update_array( $value ) );
        }
        catch ($e) {
            warn "Error in updating value ($value): $e\n";
        }
        return 1;
    }
    else {
        my $new_value = prompt("Change $name to : ", '-tty') . '';
        try {
            $plugin->$writer( $new_value );
        }
        catch ($e) {
            if ( $e =~ /ArrayRefHashRef/ ) {
                $plugin->$writer( [{ regex => qr/$new_value/, enabled => 1 }] );
            }
            else {
                warn "Could not work out how to add this value: $e";
            }
        }
        return 1;
    }
    return 0;
}

sub update_array {
    my ($array) = @_;

    my $i = 0;
    for my $element ( @{ $array } ) {
        printf "%2d  Update %s\n", $i++, show_value($element);
    }

    print <<"MENU";
 a  Add new element
 d  Delete element
 r  Return to previous menu
MENU

    my $answer = prompt_menu( 0 .. $i - 1, qw/a d r R/ );

    return $array if !defined $answer || lc $answer eq 'r';

    if ( $answer eq 'd' ) {
        my $delete = prompt("Delete which entry : ", '-tty');
        if ( $delete == 0 ) {
            shift @{ $array };
        }
        elsif ( $delete == @{ $array } - 1 ) {
            pop @{ $array };
        }
        else {
            $array = [ @{ $array }[ 0 .. $delete - 1 ], @{ $array }[ $delete + 1 .. @{ $array } - 1 ] ];
        }
    }
    elsif ( $answer eq 'a' ) {
        my $new = prompt("Enter new element : ", '-tty') . '';
        push @{ $array }
            , $new =~ / ^ (?: qr | \[ | \{ ) /xms
            ? eval $new
            : $new;
    }
    else {
        $array->[$answer]
            = ref $array->[$answer] eq 'ARRAY'  ? update_array( $array->[$answer] )
            : ref $array->[$answer] eq 'HASH'   ? update_hash( $array->[$answer] )
            : ref $array->[$answer] eq 'Regexp' ? update_regex( $array->[$answer] )
            :                                     prompt("Enter new value : ", '-tty') . '';
    }

    return $array;
}

sub update_hash {
    my ( $hash ) = @_;

    die "Not done yet";
}

sub update_regex {
    my ( $regex ) = @_;

    $regex = prompt("Enter new value : ", '-tty');
    return qr/$regex/;
}

sub show_value {
    my ($value) = @_;

    if ( !ref $value ) {
        return $value;
    }
    elsif ( ref $value eq 'ARRAY' ) {
        return '[ ' . ( join ', ', map { show_value($_) } @{ $value } ) . ' ]';
    }
    elsif ( ref $value eq 'HASH' ) {
        return '{ ' . ( join ', ', map { "$_=>" . show_value($value->{$_}) } keys %{ $value } ) . ' }';
    }
    elsif ( ref $value eq 'Regexp' ) {
        return "qr/$value/";
    }
    else {
        warn "Don't yet display " . ( ref $value ) . " values\n";
    }

    return '';
}

sub prompt_menu {
    my @choices = @_;
    my @onechar = ('-one_char');
    for my $choice (@choices) {
        @onechar = () if length $choice > 1;
    }
    my $match
        = @onechar
        ? '^[' . ( join '',  @choices ) . ']?$'
        : '^(' . ( join '|', @choices ) . ')?$';

    my $answer = prompt(
        -prompt => 'Enter your choice [' . ( join ',', @choices ) . '] ',
        @onechar,
        '-tty',
        -require => {
            'Must be one of [' . ( join ', ', @choices ) . '] ' => qr/$match/,
        },
    );
    print "\n" if @onechar;

    return $answer;
}

__DATA__

=head1 NAME

tailt - Tail files using the Tail::Tool library

=head1 VERSION

This documentation refers to tailt version 0.1.

=head1 SYNOPSIS

   tailt [option]

 OPTIONS:
  -o --other         other option

  -v --verbose       Show more detailed option
     --version       Prints the version information
     --help          Prints this help information
     --man           Prints the full documentation for tailt



=head1 DESCRIPTION

A full description of the module and its features.

May include numerous subsections (i.e., =head2, =head3, etc.).


=head1 SUBROUTINES/METHODS

A separate section listing the public components of the module's interface.

These normally consist of either subroutines that may be exported, or methods
that may be called on objects belonging to the classes that the module
provides.

Name the section accordingly.

In an object-oriented module, this section should begin with a sentence (of the
form "An object of this class represents ...") to give the reader a high-level
context to help them understand the methods that are subsequently described.

=head1 DIAGNOSTICS

A list of every error and warning message that the module can generate (even
the ones that will "never happen"), with a full explanation of each problem,
one or more likely causes, and any suggested remedies.

=head1 CONFIGURATION AND ENVIRONMENT

A full explanation of any configuration system(s) used by the module, including
the names and locations of any configuration files, and the meaning of any
environment variables or properties that can be set. These descriptions must
also include details of any configuration language used.

=head1 DEPENDENCIES

A list of all of the other modules that this module relies upon, including any
restrictions on versions, and an indication of whether these required modules
are part of the standard Perl distribution, part of the module's distribution,
or must be installed separately.

=head1 INCOMPATIBILITIES

A list of any modules that this module cannot be used in conjunction with.
This may be due to name conflicts in the interface, or competition for system
or program resources, or due to internal limitations of Perl (for example, many
modules that use source code filters are mutually incompatible).

=head1 BUGS AND LIMITATIONS

A list of known problems with the module, together with some indication of
whether they are likely to be fixed in an upcoming release.

Also, a list of restrictions on the features the module does provide: data types
that cannot be handled, performance issues and the circumstances in which they
may arise, practical limitations on the size of data sets, special cases that
are not (yet) handled, etc.

The initial template usually just has:

There are no known bugs in this module.

Please report problems to Ivan Wills (ivan.wills@gamil.com).

Patches are welcome.

=head1 AUTHOR

Ivan Wills - (ivan.wills@gamil.com)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2010 Ivan Wills (14 Mullion Close, Hornsby Heights, NSW, Australia, 2077).
All rights reserved.

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L<perlartistic>.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.

=cut
